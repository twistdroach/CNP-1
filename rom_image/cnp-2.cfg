# $0000-$01FF - RAM (zero page and stack space)
# $0200-$020F - IO Device #1 (16 bytes) VIA2
# $0210-$021F - IO Device #2 (16 bytes) VIA1
# $0220-$022F - IO Device #3 (16 bytes) ACIA
# $0230-$023F - IO Device #4 (16 bytes) Expansion Port
# $0240-$02FF - unassigned (can be externally decoded for use) 
# $0300-$7FFF - RAM
# $8000-$FFFF - ROM

SYMBOLS
{
    __ACIA_BASE__: value=$0220, type = weak;
    __VIA1_BASE__: value=$0210, type = weak;
    __VIA2_BASE__: value=$0200, type = weak;
    __EXP_PORT_BASE__: value=$0230, type = weak;
}

MEMORY
{
    ZP: start=$0, size=$100, type=rw, define=yes;
    RAM: start=$0300, size=$0CFF, type=rw, define=yes;
    USERRAM: start=$1000, size=$6FFF, type=rw, define=yes;
    ROM: start=$8000, size=$8000, type=ro, define=yes, fill=yes, file=%O;
}

SEGMENTS
{
    ZEROPAGE: load=ZP, type=zp, define=yes;
    DATA: load=ROM, run=RAM, type=rw, define=yes;
    RODATA: load=ROM, run=ROM, type=ro, align=$0100;
    BSS: load=RAM, type=bss, align=$0100;
    CODE: load=ROM, type=ro, align=$0100;
    DISASMCODE: load=ROM, type=ro, align=$0100;
    BIOSJMPTABLE: load=ROM, run=ROM, type=ro, offset=$7f00;
    VECTORS: load=ROM, type=ro, offset=$7ffa;
}
